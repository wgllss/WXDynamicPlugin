plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
}

android {
    compileSdk libs.versions.compileSdk.get().toInteger()
    namespace "com.wgllss.sample.home"

    defaultConfig {
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdkVersion.get().toInteger()
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    compileOnly project(path: ':Maven-Wgllss-Dynamic-Plugin-Common-Library')
    compileOnly project(path: ':maven-wgllss-common-re-library')

    compileOnly project(path: ':maven-wgllss-sample-business-library')
    compileOnly project(path: ':maven-wgllss-business-re-library')

    compileOnly project(path: ':maven-wgllss-sample-ui-other-lib')
    compileOnly project(path: ':Maven-Wgllss-Dynamic-Plugin-Manager')

    compileOnly 'io.github.wgllss:Wgllss-Dynamic-Host-Home-Lib:1.0.0.7'
    compileOnly 'io.github.wgllss:Wgllss-Dynamic-Host-Provider-Lib:1.0.0.6'

    compileOnly libs.androidx.sqlite.framework
    compileOnly libs.androidx.core.ktx
    implementation libs.appcompat
    implementation libs.androidx.recyclerview
    implementation libs.google.material

    compileOnly libs.androidx.lifecycle.runtime.ktx
    compileOnly libs.androidx.activity.ktx
    compileOnly libs.androidx.fragment.ktx

    compileOnly libs.converter.gson
    compileOnly libs.mmkv
    compileOnly libs.startup.runtime
    compileOnly libs.androidx.legacy.legacy.support.v44
}


def createCopyTask(buildType) {
    def workingDirPath = rootProject.ext.workingDirPath
    def jdkDirPath = rootProject.ext.jdkDirPath
    def outputFile = file("${workingDirPath}classes_home.jar")
    def outputDexFile = file("${workingDirPath}classes_home_dex.jar")
    def lastOutputDexFile = file("${rootProject.getBuildDir()}/classes_home_dex")
    if (lastOutputDexFile.exists()) {
        lastOutputDexFile.delete()
    }
    if (outputDexFile.exists()) {
        outputDexFile.delete()
    }
    if (outputFile.exists()) {
        outputFile.delete()
    }
    def inputFile = file("${getProject().getBuildDir()}/intermediates/aar_main_jar/${buildType}/sync${buildType}LibJars/classes.jar")
    def copyTask = tasks.create("assembleCopy${buildType.capitalize()}", Copy) {
        group = 'other'
        description = "复制${name}到dx环境中."
        from(inputFile.getParent()) {
            include(inputFile.name)
            rename { outputFile.name }
        }
        into(outputFile.getParent())
    }.dependsOn("assemble${buildType.capitalize()}")
    def assembleDxCommand = tasks.create("assembleDxCommand", Exec) {
        group = 'other'
        description = "${name}到dx执行中..."
        workingDir workingDirPath
        def javaExe = new File("$jdkDirPath", "bin/java").absoluteFile
        def d8JarPath = new File(workingDirPath, "lib/d8.jar").absolutePath
        def outputJar = new File(workingDirPath, outputFile.name).absolutePath
        def outputDex = new File(workingDirPath, outputDexFile.name).absolutePath

        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
            it.commandLine 'cmd', '/c', javaExe, '-cp', d8JarPath, 'com.android.tools.r8.D8', '--output', outputDex, outputJar
//            it.commandLine 'cmd', '/c', "d8 --output ${outputDexFile.name} ${outputFile.name}"
        } else {
            it.commandLine 'bash', '-c', javaExe, '-cp', d8JarPath, 'com.android.tools.r8.D8', '--output', outputDex, outputJar
//            it.commandLine 'bash', '-c', "d8 --output ${outputDexFile.name} ${outputFile.name}"
        }
//        it.commandLine 'cmd', "/c", "d8 --output ${outputDexFile.name} ${outputFile.name}"
    }.dependsOn(copyTask.name)

    return tasks.create("assembleDxCommandAndCopy") {
        doLast {
            copy {
                from(outputDexFile.getParent()) {
                    include(outputDexFile.name)
                    rename { lastOutputDexFile.name }
                }
                into(lastOutputDexFile.getParent())
            }
        }
    }.dependsOn(assembleDxCommand.name)
}

tasks.whenTaskAdded { task ->
//    if (task.name == "assembleDebug") {
//        createCopyTask("Debug")
//    }
    if (task.name == "assembleRelease") {
        createCopyTask("Release")
    }
}