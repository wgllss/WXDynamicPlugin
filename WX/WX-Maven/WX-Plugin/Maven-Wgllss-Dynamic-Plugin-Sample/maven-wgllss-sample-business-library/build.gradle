plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.kotlin.kapt)
}

android {
    compileSdk libs.versions.compileSdk.get().toInteger()
    namespace "com.wgllss.sample.library"

    defaultConfig {
        minSdkVersion libs.versions.minSdk.get().toInteger()
        targetSdkVersion libs.versions.targetSdkVersion.get().toInteger()
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    kapt {
        generateStubs = true
    }
}

dependencies {
    implementation project(':maven-wgllss-dynamic-host-skin-resource-lib')
    compileOnly project(':Maven-Wgllss-Dynamic-Plugin-Common-Library')
    compileOnly project(':maven-wgllss-common-re-library')
    compileOnly project(':maven-wgllss-business-re-library')

    compileOnly libs.androidx.core.ktx
    compileOnly libs.androidx.lifecycle.runtime.ktx
    compileOnly libs.androidx.lifecycle.livedata.ktx
    compileOnly libs.androidx.lifecycle.viewmodel.ktx
//    compileOnly libs.androidx.activity.ktx
//    compileOnly libs.androidx.fragment.ktx

    compileOnly libs.org.jsoup.jsoup
    compileOnly libs.squareup.retrofit
    compileOnly libs.com.squareup.okhttp3.logging.interceptor
    compileOnly libs.okhttp3.okhttp
    compileOnly libs.okio
    compileOnly libs.converter.gson
    compileOnly libs.converter.scalars

    compileOnly libs.mmkv
    compileOnly libs.palette.ktx
    //room 数据库
    compileOnly libs.glide
    compileOnly libs.room.runtime
    kapt libs.room.compiler

    compileOnly libs.exoplayer.core
//    compileOnly libs.exoplayer.ui
    compileOnly libs.google.extension.mediasession

    compileOnly libs.androidx.legacy.legacy.support.v44
}


def createCopyTask(buildType) {
    def workingDirPath = rootProject.ext.workingDirPath
    def outputFile = file("${workingDirPath}classes_business_lib.jar")
    def outputDexFile = file("${workingDirPath}classes_business_lib_dex.jar")
    def lastOutputDexFile = file("${rootProject.getBuildDir()}/classes_business_lib_dex")
    if (lastOutputDexFile.exists()) {
        lastOutputDexFile.delete()
    }
    if (outputDexFile.exists()) {
        outputDexFile.delete()
    }
    if (outputFile.exists()) {
        outputFile.delete()
    }
    def inputFile = file("${getProject().getBuildDir()}/intermediates/aar_main_jar/${buildType}/sync${buildType}LibJars/classes.jar")
    def copyTask = tasks.create("assembleCopy${buildType.capitalize()}", Copy) {
        group = 'other'
        description = "复制${name}到dx环境中."
        from(inputFile.getParent()) {
            include(inputFile.name)
            rename { outputFile.name }
        }
        into(outputFile.getParent())
    }.dependsOn("assemble${buildType.capitalize()}")
    def assembleDxCommand = tasks.create("assembleDxCommand", Exec) {
        group = 'other'
        description = "${name}到dx执行中..."
        workingDir workingDirPath
        if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
            it.commandLine 'cmd', '/c', "d8 --output ${outputDexFile.name} ${outputFile.name}"
        } else {
            it.commandLine 'bash', '-c ', "d8 --output ${outputDexFile.name} ${outputFile.name}"
        }
//        it.commandLine 'cmd', "/c", "d8 --output ${outputDexFile.name} ${outputFile.name}"
    }.dependsOn(copyTask.name)

    return tasks.create("assembleDxCommandAndCopy") {
        doLast {
            copy {
                from(outputDexFile.getParent()) {
                    include(outputDexFile.name)
                    rename { lastOutputDexFile.name }
                }
                into(lastOutputDexFile.getParent())
            }
        }
    }.dependsOn(assembleDxCommand.name)
}

tasks.whenTaskAdded { task ->
//    if (task.name == "assembleDebug") {
//        createCopyTask("Debug")
//    }
    if (task.name == "assembleRelease") {
        createCopyTask("Release")
    }
}